<span style="display:none;">
%%[

SET @SubscriberKey = _SubscriberKey
SET @CurrentYear = Format(Now(), "yyyy")
SET @Email = emailaddr

/* From Contacts We get HasOptedOutOfEmail and et4ae5__HasOptedOutOfMobile__c and Status from Subscriber */

SET @recordData = RetrieveSalesforceObjects("Contact", "HasOptedOutOfEmail, et4ae5__HasOptedOutOfMobile__c", "Id", "=", @SubscriberKey)

IF RowCount(@recordData) > 0 THEN
    SET @HasOptedOutOfEmail = Field(Row(@recordData, 1), "HasOptedOutOfEmail")
    SET @HasOptedOutOfMobile = Field(Row(@recorddata, 1), "et4ae5__HasOptedOutOfMobile__c")
ENDIF

/* Get Subscription status from Subscriber */

SET @rr = CreateObject("RetrieveRequest")
SetObjectProperty(@rr, "ObjectType", "Subscriber")
AddObjectArrayItem(@rr, "Properties", "Status")

SET @filter = CreateObject("SimpleFilterPart")
SetObjectProperty(@filter, "Property", "SubscriberKey")
SetObjectProperty(@filter, "SimpleOperator", "equals")
AddObjectArrayItem(@filter, "Value", @subscriberKey)

SetObjectProperty(@rr, "Filter", @filter)

SET @results = InvokeRetrieve(@rr)

IF RowCount(@results) > 0 THEN
  SET @subStatus = Field(Row(@results,1), "Status")
ENDIF


/* Define Checkboxes values */

IF @subStatus == "Unsubscribed" THEN
  SET @CheckEmailName = "EmailResubscribe"
  SET @CheckEmailLabel = "Please resubscribe me to emails"
ELSE
  SET @CheckEmailName = "EmailUnsubscribe"
  SET @CheckEmailLabel = "Please unsubscribe me from emails"
ENDIF

IF @HasOptedOutOfMobile == "true" THEN
  SET @CheckSMSName = "SMSResubscribe"
  SET @CheckSMSLabel = "Please resubscribe me to text messages"
ELSE
  SET @CheckSMSName = "SMSUnsubscribe"
  SET @CheckSMSLabel = "Please unsubscribe me from text messages"
ENDIF

IF @subStatus == "Unsubscribed" AND @HasOptedOutOfMobile == "true" THEN
  SET @CheckAllName = "ALLResubscribe"
  SET @CheckAllLabel = "Please resubscribe me to everything"
ELSE
  SET @CheckAllName = "ALLUnsubscribe"
  SET @CheckAllLabel = "Please unsubscribe me from everything"
ENDIF


/* Check Form Data POST Submission */

SET @Submit = RequestParameter("Submit")

IF @Submit == "true" THEN
  SET @SubscriberKey = RequestParameter("SubscriberKey")
  SET @Email = RequestParameter("Email")
  SET @EmailUnsubscribe = RequestParameter("EmailUnsubscribe")
  SET @SMSUnsubscribe = RequestParameter("SMSUnsubscribe")
  SET @ALLUnsubscribe = RequestParameter("ALLUnsubscribe")

  SET @EmailResubscribe = RequestParameter("EmailResubscribe")
  SET @SMSResubscribe = RequestParameter("SMSResubscribe")
  SET @AllResubscribe = RequestParameter("ALLResubscribe")

ENDIF

IF @ALLUnsubscribe == "on" THEN
  SET @Action = "Unsubscribe from Email And SMS"

  /* log unsubscribe event to mark as unsubscribed in All Subscribers */
  SET @reason = "Unsubscribed through custom subscription center"

  SET @lue = CreateObject("ExecuteRequest")
  SETObjectProperty(@lue,"Name","LogUnsubEvent")

  SET @lue_prop = CreateObject("APIProperty")
  SETObjectProperty(@lue_prop, "Name", "SubscriberKey")
  SETObjectProperty(@lue_prop, "Value", @SubscriberKey)
  AddObjectArrayItem(@lue, "Parameters", @lue_prop)

  SET @lue_prop = CreateObject("APIProperty")
  SETObjectProperty(@lue_prop, "Name", "Reason")
  SETObjectProperty(@lue_prop, "Value", @reason)
  AddObjectArrayItem(@lue, "Parameters", @lue_prop)

  SET @lue_statusCode = InvokeExecute(@lue, @overallStatus, @requestId)

  SET @updateRecord = UpdateSingleSalesforceObject(
      "Contact", @SubscriberKey,
      "et4ae5__HasOptedOutOfMobile__c", "true",
      "HasOptedOutOfEmail", "true"
     )

ELSEIF @EmailUnsubscribe == "on" THEN
  SET @Action = "Unsubscribe only from Email"

  /* log unsubscribe event to mark as unsubscribed in All Subscribers */
  SET @reason = "Unsubscribed through custom subscription center"

  SET @lue = CreateObject("ExecuteRequest")
  SETObjectProperty(@lue,"Name","LogUnsubEvent")

  SET @lue_prop = CreateObject("APIProperty")
  SETObjectProperty(@lue_prop, "Name", "SubscriberKey")
  SETObjectProperty(@lue_prop, "Value", @SubscriberKey)
  AddObjectArrayItem(@lue, "Parameters", @lue_prop)

  SET @lue_prop = CreateObject("APIProperty")
  SETObjectProperty(@lue_prop, "Name", "Reason")
  SETObjectProperty(@lue_prop, "Value", @reason)
  AddObjectArrayItem(@lue, "Parameters", @lue_prop)

  SET @lue_statusCode = InvokeExecute(@lue, @overallStatus, @requestId)

  SET @updateRecord = UpdateSingleSalesforceObject(
    "Contact", @SubscriberKey,
    "HasOptedOutOfEmail", "true"
  )

ELSEIF @SMSUnsubscribe == "on" THEN
  SET @Action = "Unsubscribe only from SMS"

  SET @updateRecord = UpdateSingleSalesforceObject(
      "Contact", @SubscriberKey,
      "et4ae5__HasOptedOutOfMobile__c", "true"
     )

ELSEIF @AllResubscribe == "on" THEN
  SET @Action = "Resubscribe to everything"

  SET @Subscriber = CreateObject("Subscriber")
   SetObjectProperty(@Subscriber, "SubscriberKey", @SubscriberKey)
   SetObjectProperty(@Subscriber, "EmailAddress", @email)
   SetObjectProperty(@Subscriber, "Status", "Active" )
   SET @Status = InvokeUpdate(@Subscriber, @createErrDesc, @createErrNo, @createOpts)

  SET @updateRecord = UpdateSingleSalesforceObject(
    "Contact", @SubscriberKey,
    "et4ae5__HasOptedOutOfMobile__c", "false",
    "HasOptedOutOfEmail", "false"
    )


ELSEIF @EmailResubscribe == "on" THEN
  SET @Action = "Resubscribe only to email"

      SET @Subscriber = CreateObject("Subscriber")
   SetObjectProperty(@Subscriber, "SubscriberKey", @SubscriberKey)
   SetObjectProperty(@Subscriber, "EmailAddress", @email)
   SetObjectProperty(@Subscriber, "Status", "Active" )
   SET @Status = InvokeUpdate(@Subscriber, @createErrDesc, @createErrNo, @createOpts)

   SET @updateRecord = UpdateSingleSalesforceObject(
      "Contact", @SubscriberKey,
      "HasOptedOutOfEmail", "false"
     )

ELSEIF @SMSResubscribe == "on" THEN
  SET @Action = "Resubscribe only to SMS"

   SET @updateRecord = UpdateSingleSalesforceObject(
      "Contact", @SubscriberKey,
      "et4ae5__HasOptedOutOfMobile__c", "false"
     )
ENDIF

]%%
</span>



<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
  <title>Eden Tyres Preference Center</title>
  <link rel='stylesheet' href='https://cdn.jsdelivr.net/npm/bootstrap@5.0.0-beta2/dist/css/bootstrap.min.css'>
  <!--<link rel="stylesheet" href="css/style.css">-->
  <link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.0.13/css/all.css" integrity="sha384-DNOHZ68U8hZfKXOrtjWvjxusGo9WQnrNx2sqG0tfsghAvtVlRW3tvkXWZh58N9jp" crossorigin="anonymous">

  <!-- replace with your favicon png 16x16 or delete-->
  <link rel="icon" href="http://image.mail.edentyres.co.uk/lib/fe3d11717564047d7c1572/m/1/3f7d241f-058c-410f-8f32-d2533c2c91a8.jpg">
  <meta name="robots" content="noindex">
<style>
    @import url('https://fonts.googleapis.com/css2?family=Roboto:ital,wght@0,100..900;1,100..900&display=swap');

    html, body {
    height: 100%;
    background-color: #000000;
    overflow-x: hidden;
    background-image: url("http://image.mail.edentyres.co.uk/lib/fe3d11717564047d7c1572/m/1/944375f2-735c-4c73-b6b3-ee6438647491.jpg"); 
    background-position: center;
    background-size: cover;
}

  *:focus {
  outline: none !important;
  box-shadow: none !important;
  border-color: #ED1C24 !important;
  }

  .logo {
    width: 150px;
  }

  /* Overrride checkbox styles */
  .check {
  border-radius: 50px;
  padding: 100px !important;
  width: 1em;
  height: 1rem;
  display: inline-block;
  accent-color: #ed1c24;
  }


  p, label, .input-group-text {
    font-size: 14px;
  }

.button {
  border:none;
  display: block;
  text-align: center;
  width: 200px !important; 
  margin-top: 20px !important;  
  }

 a:hover {
  color: #fff;
 } 

.button:hover {
  background-color: #6b1013 !important;
  transition: 0.5s;
  }


.nav-title {
  text-align:center;
  color:#666666;
  font-size: 24px;
  font-weight:600;
  }




.logo-section {
  margin:0px;
  background-color:#ffffff;
  padding-bottom:0px;
  box-shadow: rgba(100, 100, 111, 0.2) 0px 7px 29px 0px;
  } 



.main-row {
    background-color:#ffffff;
    margin: 70px auto 70px auto;
    max-width:650px;
    border-radius:10px; 
    padding: 20px;
  }

h2 {
    font-family: "Roboto", sans-serif;
    font-weight: 700;
    padding:20px 0px 10px 0px;
    color: #666666;  
  }

.red {
    color: red;
  }


.button {
  background-color:#ed1c24;
  color:#ffffff;
  padding:15px 25px;
  border-radius:5px;
  margin: 10px 0px;
  font-weight: 600;
  }

  .footer {
    background-color:#1b1b1b;
    color: #fff; 
    margin: 0;
    text-align: center;
    padding: 15px 0px;
  }


  @media (min-width: 768px) {

  p, label, .input-group-text {
  font-size: 16px;
  }

  body {
  min-height: 100vh;
  margin:0; }

  .footer {
    min-height:70px;
  }

  body {
    display:flex;
    flex-direction:column; }

  .main {
    flex:1; }
  }
</style>
</head>
<body >
  

<!-- logo section -->
<div class="container-full logo-section">
  <div class="container">
    <div class="row .mb-0" >
      <div class="col-12 col-md-6 d-flex justify-content-md-start justify-content-center align-self-center"  >
        <a href="https://www.edentyres.com/" target="_blank">
          <img class="logo" style="padding:10px 0px" src="http://image.mail.edentyres.co.uk/lib/fe3d11717564047d7c1572/m/1/ac96d0d2-b86e-42d9-8ec7-c6e422535afc.png" alt="Eden Tyres"></a>
      </div>
      <div class="col-12 col-md-6 d-flex justify-content-md-end justify-content-center align-self-center logo-title">
        <p class="nav-title" style="padding-top:10px">Communication Preferences</p>
      </div>
    </div>
  </div>
</div>
<!-- End of logo section -->

<!-- Preview Testing Data -->
 <div style="background-color:red;color:#fff">
<p style="padding:20px;">SubscriberKey: %%=v(@SubscriberKey)=%%<br>
Has Opted Out of Email: %%=v(@HasOptedOutOfEmail)=%%<br>
Has Opted Out of Mobile: %%=v(@HasOptedOutOfMobile)=%%<br>
Subscriber Status: %%=v(@subStatus)=%%<br>
Email: %%=v(@Email)=%%
</p>

%%[IF @Submit == "true" THEN ]%%
<p style="padding:20px;">
SubscriberKey: %%=v(@SubscriberKey)=%%<br>
Email Checkbox: %%=v(@checkEmail)=%%<br>
SMS Checkbox: %%=v(@checkSMS)=%%<br>
All Checkbox: %%=v(@checkAll)=%%<br>
Action: %%=v(@Action)=%%

</p>

%%[ ENDIF ]%%

 </div>

<!-- End of Preview Testing Data -->


%%[ IF @Submit != "true" THEN ]%%

<!-- Profile Center Form -->
<div class="container p-4 main" >
  <div class="row main-row">
    <div class="col-12 pt-4" >


      <h2>Update your Profile:</h2>
      <p>Update your details or add/remove a vehicle so we only send reminders
that are useful to you.</p>
<div>
<a class="button" target="_blank" style="text-decoration:none; margin-top: 20px" href="https://ets.my.site.com/customerportal">Update Profile</a>
</div>


    </div>


    <div class="col-12">
      <h2>Update your preferences:</h2>
      <p>If you'd prefer not to receive MOT or Service reminders, or the occasional
exclusive offer, you can update your preferences here.</p>
<form method="post" action="https://cloud.mail.edentyres.co.uk/prefcenter" >
    <div >
        <input type="checkbox" checked class="check" name="%%=v(@CheckEmailName)=%%">
        <label >%%=v(@CheckEmailLabel)=%%</label>     
    </div>
    <div >
        <input type="checkbox" checked class="check" name="%%=v(@CheckSMSName)=%%">
        <label class="form-check-label" for="exampleCheck1">%%=v(@CheckSMSLabel)=%%</label>     
    </div>
    <div >
        <input type="checkbox" checked class="check" name="%%=v(@CheckAllName)=%%">
        <label class="form-check-label" for="exampleCheck1">%%=v(@CheckAllLabel)=%%</label>     
    </div>
  </div>
  <div class="col-12 pt-2">
    <button type="submit" class="button">Update Preferences</button>
  </div>
    <input type="hidden" name="Submit" value="true">
    <input type="hidden" name="SubscriberKey" value="%%=v(@SubscriberKey)=%%">
    <input type="hidden" name="Email" value="%%=v(@Email)=%%">
  </form>
  </div> 
</div>    
<!--End of Profile Center Form--> 




%%[ ELSE ]%%
<!-- Profile Center Form -->
<div class="container p-4 main" >
  <div class="row main-row">
    <div class="col-12 pt-4" >
      <h2>Thank you!</h2>
      <p>Your preferences have been updated!</p>
    </div>
  </div>  
</div>
<!--End of Profile Center Form--> 
%%[ ENDIF ]%%


<div class="container-full footer" >
  <div class="row">
    <div class="col-12">
      <p>Copyright Eden Tyres & Servicing %%=v(@CurrentYear)=%%</p>
    </div>
  </div>
</div>

</body>



</html>
